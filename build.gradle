plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id "org.sonarqube" version "2.7.1"
    id 'checkstyle'
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

group 'com.kirekov'
version '1.3.1'
description 'Just some useful utils for everyday coding'

checkstyle {
    configFile = file('codestyle/checkstyle.xml')
    ignoreFailures = false
    maxWarnings = 0
    showViolations = true
    toolVersion = '8.42'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'
    testImplementation 'org.awaitility:awaitility:3.1.6'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.1'
}

test {
    useJUnitPlatform()
}

task testsJar(type: Jar) {
    archiveClassifier = 'tests'
    from sourceSets.test.output
}

publishing {
    publications {
        javaUsefulUtils(MavenPublication) {
            artifact jar {
                extension 'jar'
            }
            artifact javadocJar {
                classifier 'javadoc'
                extension 'jar'
            }
            artifact sourcesJar {
                classifier 'sources'
                extension 'jar'
            }
            artifact testsJar {
                classifier 'tests'
                extension 'jar'
            }
            pom {
                name = 'Java Useful Utils'
                description = 'Just some useful utils for everyday coding'
                url = 'https://github.com/SimonHarmonicMinor/Java-Useful-Utils'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://www.mit.edu/~amini/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'kirekov'
                        name = 'Semyon Kirekov'
                        email = 'semyon@kirekov.com'
                    }
                }
                scm {
                    connection = 'scm:https://github.com/SimonHarmonicMinor/Java-Useful-Utils.git'
                    developerConnection = 'scm:git://github.com/SimonHarmonicMinor/Java-Useful-Utils.git'
                    url = 'https://github.com/SimonHarmonicMinor/Java-Useful-Utils'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username findProperty('ossSonatypeUsername')
                password findProperty('ossSonatypePassword')
            }
        }
    }
}

signing {
    sign publishing.publications.javaUsefulUtils
}

nexusPublishing {
    repositories {
        sonatype {
            username = findProperty('ossSonatypeUsername')
            password = findProperty('ossSonatypePassword')
        }
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "SimonHarmonicMinor_Java-Useful-Utils"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
    }
}